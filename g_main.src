// ////////////////////////////////////////////////////////////////////////////
// Core and general game systems
// ////////////////////////////////////////////////////////////////////////////

// ////////////////////////////////////////////////////////////////////////////
// Constant definitions
// ////////////////////////////////////////////////////////////////////////////

declare proSubject    0
declare proObject     1
declare proPos        2
declare proAdject     3
declare proReflex     4

declare skillMelee    0
declare skillRanged   1
declare skillMagic    2

declare dtBlunt       "blunt"
declare dtCutting     "cutting"
declare dtPiercing    "piercing"
declare dtFire        "fire"
declare dtAir         "air"
declare dtEarth       "earth"
declare dtWater       "water"

// ////////////////////////////////////////////////////////////////////////////
// Common strings
// ////////////////////////////////////////////////////////////////////////////

declare hoursPerDay       1440
declare minutesPerHour    60
declare continueStr       "Continue"

object sexNeuter
    article             "a "
    name                "neuter"
    pronouns            [ "it" "it" "its" "its" "itself" ]
;
object sexMale
    article             "a "
    name                "male"
    pronouns            [ "he" "him" "his" "his" "himself" ]
;
object sexFemale
    article             "a "
    name                "female"
    pronouns            [ "she" "her" "her" "hers" "herself" ]
;

object speciesHuman
    article             "a "
    name                "human"
;
object speciesGnoll
    article             "a "
    name                "gnoll"
;
object speciesDragon
    article             "a "
    name                "dragon"
;

object thePlayer
    article             "the "
    name                "player"
    sex                 sexNeuter
    species             speciesHuman
    faction             0
    inventory           {}
    skills {
        skillMelee:     10
        skillRanged:    12
        skillMagic:     8
    }
;

object world
    time                720 // start game at noon
    location            0
    locationName        0
    inLocation          0
    newLocation         0
;


// ////////////////////////////////////////////////////////////////////////////
// Utility functions
// ////////////////////////////////////////////////////////////////////////////

asm_function addContinue() {
    continueStr $location world get_prop add_option
}

function dispatch(event extra : result) {
    (if (neq (typeof event) Object)
        (store event (get_prop world $location)))
    (while 1
        (proc
            (store result ((get_prop event $body) extra))
            (if (neq (typeof result) Object) (break))
            (store event result)
            (store extra 0)))

    (set_prop world $inLocation false)
    (set_prop world $newLocation false)

    (store result (get_prop event $location))
    (if (neq result 0)
        (proc
            (set_prop world $inLocation true)
            (if (neq event (get_prop world $location))
                (proc
                    (set_prop world $newLocation true)
                    (set_prop world $location event)
                    (set_prop world $locationName result)
                    (set_setting infobarLeft result)))))

//    "\nIN LOCATION: " say $inLocation world get_prop say
//    "\nNEW LOCATION: " say $newLocation world get_prop say
//    "\nLOCATION NAME: " say $locationName world get_prop say
    (get_option dispatch)
}

asm_function main() {
    "Caged Gnoll" infobarTitle set_setting
    start 1 dispatch call
}


asm_function sayA(theObject upperFirst) {
    $article theObject get_prop say
    $name theObject get_prop say
}
asm_function sayThe(theObject upperFirst) {
    "the " say
    $name theObject get_prop say
}
asm_function sayName(theObject upperFirst) {
    $name theObject get_prop say
}
asm_function sayPronoun(who thePronoun upperFirst) {
    thePronoun $pronouns $sex who get_prop get_prop get_item
    upperFirst asUpper jnz say return asUpper: say_uf
}

function skillCheck(who skill modifier : roll total) {
    (if (eq (typeof modifier) None)
        (store modifier 0))
    (store skill (get_item (get_prop who $skills) skill))
    ("[(" skill "+" modifier ")=" (add skill modifier) " vs (")

    (store roll (random 1 7))   (print roll "+")    (store total roll)
    (store roll (random 1 7))   (print roll "+")    (store total (add total roll))
    (store roll (random 1 7))   (print roll ")=")   (store total (add total roll))
    (print total)

    (if (gte (add skill modifier) total)
        (proc
            (" = success]")
            (return 1))
        (proc
            (" = fail]"
            (return 0))))
}


// ////////////////////////////////////////////////////////////////////////////
// Inventory functions
// ////////////////////////////////////////////////////////////////////////////

asm_function addItems(item qty : invMap) {
    $inventory thePlayer get_prop *invMap store
    item invMap has_item alreadyHas jnz
    qty item invMap set_item
    1 return

    alreadyHas:
    item invMap get_item qty add item invMap set_item
    1 return
}

asm_function removeItems(item qty : invMap) {
    item 1 itemQty call stack_dup qty lt notEnough jnz
    $inventory thePlayer get_prop *invMap store
    qty sub item invMap set_item 1 return
    notEnough: 0 return
}

asm_function itemQty(item : invMap) {
    $inventory thePlayer get_prop *invMap store
    item invMap get_item return
}



// ////////////////////////////////////////////////////////////////////////////
// Startup and chargen
// ////////////////////////////////////////////////////////////////////////////

object start
    body asm_function() {
        "Welcome to game!\nYou can select options by clicking on them or (for options up to ten) by pressing the appropriate number key." say
        "Start game" chargen1 add_option
    }
;

object chargen1
    body asm_function() {
        "What sex are you?" say
        "Male"   chargen2   sexMale    add_option_x
        "Female" chargen2   sexFemale  add_option_x
    }
;
object chargen2
    body asm_function(choice) {
        choice $sex thePlayer set_prop

        "What species are you?" say
        "Human"     chargen3    speciesHuman   add_option_x
        "Gnoll"     chargen3    speciesGnoll   add_option_x
        "Dragon"    chargen3    speciesDragon  add_option_x
    }
;
object chargen3
    body asm_function(choice) {
        choice $species thePlayer set_prop

        50 gold 2 addItems call
        1 loincloth 2 addItems call

        "Character" pageCharacter 99 add_page
        "Inventory" pageInventory 105 add_page

        forestPath
    }
;


// ////////////////////////////////////////////////////////////////////////////
// Page handlers
// ////////////////////////////////////////////////////////////////////////////

asm_function pageCharacter() {
    "You are " say
    $sex thePlayer get_prop 1 sayA call
    " " say
    $name $species thePlayer get_prop get_prop say
    "." say

    continueStr pageDone add_option
    pageDispatch get_option
}

asm_function pageInventory(: list keys itemCount counter) {
    $inventory thePlayer get_prop *list store
    list get_keys *keys store
    keys get_size *itemCount store
    0 *counter store
    loopStart:
    counter itemCount gte done jnz
    counter 1 add say
    ") " say
    counter keys get_item 1 sayName call
    " (x" say
    counter keys get_item list get_item say
    ")[br]" say
    counter 1 add *counter store
    loopStart jmp

    done:

    continueStr pageDone add_option
    pageDispatch get_option
}

asm_function pageDispatch(event extra : result) {
    extra 1 event call
    *result store
}

asm_function pageDone() {
    end_page
}

// ////////////////////////////////////////////////////////////////////////////
// General item definitions
// ////////////////////////////////////////////////////////////////////////////

object gold
    article     "a "
    name        "gold piece"
    plural      "gold pieces"
    description "The coin of the realm."
;

object loincloth
    article     "a "
    name        "loincloth"
    plural      "loincloths"
    slot        "body"
;


// ////////////////////////////////////////////////////////////////////////////
// Generic nodes for resting
// ////////////////////////////////////////////////////////////////////////////

object restAwhile
    body asm_function() {
        "How long would you like to rest for?" say
        "One hour"      restAwhileDoit    60  add_option_x
        "Six hours"     restAwhileDoit    360 add_option_x
        "Twelve hours"  restAwhileDoit    720 add_option_x
        "Don't rest" $location world get_prop add_option
    }
;

object restAwhileDoit
    body asm_function(length) {
        "You rest for a while. " say
//        length do_rest
        0 addContinue call
    }
;


include "g_forest.src";
include "g_town.src";
